apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = "me.relex"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "me.relex"
                artifactId = 'circleindicator'
                version = this.version
                pom.withXml {
                    def dependenciesNode = asNode()['dependencies'][0] ?: asNode().appendNode(
                            'dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    configurations = ['archives']
    publications = ['release']
    override = true
    pkg {
        repo = "maven"
        name = "CircleIndicator"
        websiteUrl = 'https://github.com/ongakuer/CircleIndicator'
        vcsUrl = 'https://github.com/ongakuer/CircleIndicator.git'
        licenses = ["Apache-2.0"]
        publish = true
    }
}
